buildscript {
    ext {
        springBootVersion = '2.0.1.RELEASE'
        testDataBuilderVersion = '0.5.0'
        dockerPluginVersion = '1.2'
    }
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://repo.spring.io/snapshot' }
        maven { url 'https://repo.spring.io/milestone' }
        maven { url "https://repo.spring.io/release" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "com.github.hippoom:test-data-builder:${testDataBuilderVersion}"
        classpath "se.transmode.gradle:gradle-docker:${dockerPluginVersion}"
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'
apply plugin: 'docker'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'com.github.kbyyd24'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

mainClassName = "com.github.kbyyd24.lddstubrunner.LddStubRunnerApplication"

repositories {
    jcenter()
    mavenCentral()
    maven { url 'https://repo.spring.io/snapshot' }
    maven { url 'https://repo.spring.io/milestone' }
    maven { url "https://repo.spring.io/release" }
}


ext {
    springCloudVersion = 'Finchley.RC1'
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter')
    compile('org.springframework.cloud:spring-cloud-starter-contract-stub-runner')
    runtime("com.github.hippoom:test-data-builder:${testDataBuilderVersion}")
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

docker {
    baseImage = 'java'
}

task stubDocker(type: Docker, dependsOn: build) {
//    switchUser('root')
    addFile(file("${buildDir}/libs/${rootProject.name}-${version}.jar"), "/root/stub-runner.jar")
    exposePort(9999)
    defaultCommand(["java", "-jar", "/root/stub-runner.jar"])
}
